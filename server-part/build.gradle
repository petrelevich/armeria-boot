plugins {
    id 'java'
    id 'idea'
    id 'org.springframework.boot'
    id 'com.google.protobuf'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

def protoSrcDir = 'build/generated/sources/proto/main/java'
def grpcSrcDir = 'build/generated/sources/proto/main/grpc'


idea {
    module {
        sourceDirs += file(protoSrcDir)
        sourceDirs += file(grpcSrcDir)
    }
}

sourceSets {
    main {
        proto {
            srcDir protoSrcDir
        }
        java {
            srcDir grpcSrcDir
        }
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.20.1'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.46.0'
        }
        reactorGrpc {
            artifact = 'com.salesforce.servicelibs:reactor-grpc:1.2.3'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
            reactorGrpc {}
        }
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'com.linecorp.armeria:armeria-grpc'

    implementation 'io.projectreactor:reactor-core'
    implementation 'com.salesforce.servicelibs:reactor-grpc-stub:1.2.3'
    compileOnly 'javax.annotation:javax.annotation-api:1.3.2'

    implementation 'net.bytebuddy:byte-buddy:1.12.10'

    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation 'com.google.code.findbugs:jsr305'

    compileOnly 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'com.linecorp.armeria:armeria-junit5'
    testImplementation 'org.assertj:assertj-core'
    testImplementation 'org.junit.jupiter:junit-jupiter-api'

}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}
