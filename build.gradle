plugins {
    id 'idea'
    id 'org.springframework.boot' version '2.6.6' apply false
    id 'io.freefair.lombok' version '6.4.2' apply false
    id 'com.google.protobuf' version '0.8.18' apply false
}

idea {
    project {
        languageLevel = 17
    }
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

allprojects {
    group "ru.petrelevich"

    repositories {
        mavenCentral()
    }

    apply plugin: "io.spring.dependency-management"
    dependencyManagement {
        imports {
            mavenBom 'io.projectreactor:reactor-bom:2020.0.19'
            mavenBom 'io.netty:netty-bom:4.1.75.Final'
            mavenBom 'com.linecorp.armeria:armeria-bom:1.16.0'
            mavenBom 'io.micrometer:micrometer-bom:1.8.3'
            mavenBom 'org.junit:junit-bom:5.8.2'
        }
        dependencies {
            dependency "com.google.code.findbugs:jsr305:${Versions.jsr305}"
        }
    }

    configurations.all {
        resolutionStrategy {
            failOnVersionConflict()

            force("com.google.protobuf:protobuf-java:3.19.2")
            force("com.google.errorprone:error_prone_annotations:2.10.0")
            force("com.google.guava:guava:30.1.1-android")
        }
    }

    task managedVersions {
        doLast {
            dependencyManagement.managedVersions.each {
                println it
            }
        }
    }
}
